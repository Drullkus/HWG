buildscript {
    repositories {
        jcenter()
    	maven { url  = "https://plugins.gradle.org/m2/" }
    }
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput

plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-1.18:3.0.35"
	modImplementation "curse.maven:modmenu-308702:3472549"
    modApi("me.shedaniel.cloth:cloth-config-fabric:6.2.57") {
    	exclude(group: "net.fabricmc.fabric-api")
    }
	modImplementation 'curse.maven:TDI-245733:3556912'
}

repositories {
    maven { url "https://maven.jamieswhiteshirt.com/libs-release" }
	maven { url "https://maven.theillusivec4.top/" }
	maven {	url "https://maven.fabricmc.net/io/github/prospector/modmenu/" }
	maven { url "https://cfa2.cursemaven.com" }
    maven { url "https://maven.abusedmaster.xyz" }
    maven { url "https://server.bbkr.space/artifactory/libs-release" }
    maven { url "https://maven.siphalor.de" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/" }
    maven { url "https://maven.blamejared.com" }
    maven { url "https://plugins.gradle.org/m2/" }
}

processResources {
    inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

processResources {
	doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each { 
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

loom {
	accessWidenerPath = file("src/main/resources/hwg.accesswidener")
}

if (file('key.properties').exists()) {
    curseforge {

    	def curseProp = new Properties()
    	File secretPropsFile = file("key.properties")
    	curseProp.load(secretPropsFile.newInputStream())

    	project {
        	apiKey = curseProp.getProperty('curseKey')
        	id = "448106"
        	releaseType = "release"
        	changelogType = 'text'
        	changelog = file('CHANGELOG.txt')
			addGameVersion("1.18.1")
			addGameVersion("Fabric")
			addGameVersion("Java 17")

        	relations {
            	requiredDependency 'fabric-api'  
            	requiredDependency 'geckolib'  
            	requiredDependency 'cloth-config'  
            	//optionalDependency 'patchouli-fabric'   
        	}
        	mainArtifact(remapJar)

        	afterEvaluate {
            	uploadTask.dependsOn("remapJar")
        	}
    	}
    	options {
    		//debug = true
        	forgeGradleIntegration = false
    	}
    }
}